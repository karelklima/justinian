PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX s: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ares: <http://linked.opendata.cz/ontology/ares#>
PREFIX my: <http://>

CONSTRUCT {
my:checks my:haveRegions ?region.
my:checks my:haveMinDate ?minDate.
my:checks my:haveMaxDate ?maxDate.
my:checks my:haveStates ?stavLabel.
}

FROM <http://linked.opendata.cz/resource/dataset/coi.cz/kontroly>
FROM <http://linked.opendata.cz/resource/dataset/ares/basic>
FROM <http://linked.opendata.cz/resource/dataset/ares/rzp>

WHERE {
{
  SELECT (MAX(?date) as ?maxDate) (MIN(?date) as ?minDate)
  WHERE
  {
  ?check a s:CheckAction.
  ?check dcterms:date ?date.
  }
}
{
  SELECT DISTINCT ?region ?stavLabel
  WHERE
  {
  ?check s:location/s:addressRegion ?region.
  OPTIONAL { 
    ?check s:object ?object.
    ?object ares:stav-zivnosti ?stavZivnosti.
    ?stavZivnosti skos:prefLabel ?stavLabel.
  }
  }
}
}