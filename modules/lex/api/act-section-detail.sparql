PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX lex: <http://purl.org/lex#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX my: <http://>

PREFIX resource: <{{"param": "resource", "filter": "RemoveLTGT"}}>

CONSTRUCT {
  resource: my:type ?type.
  resource: my:hasAncestor ?ancSection.
  resource: my:actTitle ?title.
  resource: my:actId ?actId.
  resource: my:sectionExpression ?sectExpr.
  resource: dcterms:identifier ?identifier.
  ?sectExpr dcterms:valid ?sectExprDate.
  ?ancSection dcterms:identifier ?ancId.
  ?ancSection my:type ?ancType.
  ?ancSection my:hasRealization ?ancExpr.
  ?ancExpr dcterms:valid ?ancExprDate.
}

FROM <http://linked.opendata.cz/resource/dataset/legislation/cz/uz>
FROM NAMED <http://linked.opendata.cz/resource/dataset/legislation/psp.cz>

WHERE {
  resource: a ?type.
  FILTER(?type != frbr:Work)

  resource: dcterms:identifier ?identifier.

  resource: frbr:partOf+ ?ancSection.

  {?ancSection a ?ancType.} UNION { GRAPH <http://linked.opendata.cz/resource/dataset/legislation/psp.cz> {?ancSection a ?ancType.} }
  FILTER( ?ancType != frbr:Work).

  ?sectExpr frbr:realizationOf  resource: .
  ?sectExpr frbr:partOf+ ?e2 .
  GRAPH <http://linked.opendata.cz/resource/dataset/legislation/psp.cz> { ?e2 dcterms:valid ?sectExprDate. }
    
  {
    ?ancSection dcterms:identifier ?ancId.

    ?ancExpr frbr:realizationOf ?ancSection.
    ?ancExpr frbr:partOf* ?e .
    GRAPH <http://linked.opendata.cz/resource/dataset/legislation/psp.cz> { ?e dcterms:valid ?ancExprDate. }

  } UNION {
  
    GRAPH <http://linked.opendata.cz/resource/dataset/legislation/psp.cz> {
      ?ancSection a lex:Act.
      OPTIONAL {
        ?ancSection dcterms:title ?title.
        ?ancSection dcterms:identifier ?actId.
      }
    }
  
  }
}
