PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX sao: <http://salt.semanticauthoring.org/ontologies/sao#>
PREFIX salt: <http://salt.semanticauthoring.org/ontologies/sdo#>

PREFIX resource: <{{"param": "resource", "filter": "RemoveLTGT"}}>

CONSTRUCT { 
  ?decision dcterms:title ?title .
  ?decision dcterms:issued ?date .
  ?decision dcterms:subject ?subjectLabel .
}

FROM <http://linked.opendata.cz/resource/dataset/legislation/nsoud.cz>
FROM <http://linked.opendata.cz/resource/dataset/legislation/nsoud.cz/updates>
FROM NAMED <http://linked.opendata.cz/resource/dataset/legislation/psp.cz>

WHERE {
  {
    SELECT DISTINCT ?decision ?date {
      {
        ?decision ^frbr:realizationOf/salt:hasSection/salt:hasParagraph/salt:hasTextChunk/salt:hasAnnotation/sao:hasTopic resource: .
      } UNION {
        GRAPH <http://linked.opendata.cz/resource/dataset/legislation/psp.cz> {
          resource: frbr:realizationOf ?act.
        }
        ?decision ^frbr:realizationOf/salt:hasSection/salt:hasParagraph/salt:hasTextChunk/salt:hasAnnotation/sao:hasTopic ?act .
      }
    OPTIONAL { ?decision dcterms:issued ?date .}
    }
    ORDER BY DESC(?date)
    OFFSET {{"param": "offset", "default": "0"}}
    LIMIT {{"param": "limit", "default": "10"}}
  }
  ?decision dcterms:title ?title .
  OPTIONAL {
    ?decision dcterms:subject ?subject .
    BIND( REPLACE(str(?subject), '^.*decision-subject/', "") AS ?subjectLabel) .
  }
}